# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/opera_desktop.gni")
import("//chrome/browser/resources/pdf/pdf.gni")
import("//pdf/features.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

assert(enable_pdf, "enable_pdf check failed")

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  path_prefix = ""
  if (opera_desktop) {
    path_prefix = "opera/desktop/chrome_imports/"
  }
  path_mappings = [
    "chrome-extension://mhjfbmdgcfjbbpaeojofohoefgiehjai/*|" +
        rebase_path(
            "$root_gen_dir/${path_prefix}chrome/browser/resources/pdf/tsc/*",
            target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = [
    "basic_plugin_test.ts",
    "basic_test.ts",
    "beep_test.ts",
    "bookmarks_test.ts",
    "download_controls_test.ts",
    "fullscreen_test.ts",
    "gesture_detector_test.ts",
    "layout_test.ts",
    "material_elements_test.ts",
    "metrics_test.ts",
    "navigator_test.ts",
    "nobeep_test.ts",
    "page_change_test.ts",
    "params_parser_test.ts",
    "plugin_controller_test.ts",
    "post_message_proxy_test.ts",
    "scroll_with_form_field_focused_test.ts",
    "swipe_detector_test.ts",
    "test_util.ts",
    "title_test.ts",
    "touch_handling_test.ts",
    "viewer_password_dialog_test.ts",
    "viewer_pdf_sidenav_test.ts",
    "viewer_properties_dialog_test.ts",
    "viewer_thumbnail_bar_test.ts",
    "viewer_thumbnail_test.ts",
    "viewer_toolbar_test.ts",
    "viewport_scroller_test.ts",
    "viewport_test.ts",
    "whitespace_title_test.ts",
    "zoom_manager_test.ts",
  ]

  if (enable_ink) {
    in_files += [
      "annotations_feature_enabled_test.ts",
      "annotations_toolbar_test.ts",
      "viewer_toolbar_dropdown_test.ts",
    ]
  }

  if (is_chromeos_ash) {
    in_files += [ "printing_icon_test.ts" ]
  }

  definitions =
      ts_definitions + [ "//tools/typescript/definitions/chrome_test.d.ts" ]
  # Even if //chrome/browser/resources/pdf:build_ts is redirected to
  # chrome_imports we need to use explicit path here because it is used by
  # tools/typescript/ts_library.gni to construct relative paths in generated
  # tsconfig.json
  deps = [
    "../webui:build_ts",
    "//${path_prefix}chrome/browser/resources/pdf:build_ts",
  ]
  extra_deps = [ "../webui:generate_definitions" ]
}

generate_grd("build_grdp") {
  grd_prefix = "pdf"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "pdf"
}
